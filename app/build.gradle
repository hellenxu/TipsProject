apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.six.tipsproject"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode gitVersion()
        versionName "1.0"
        manifestPlaceholders = [APP_TAG: 'release']
        buildConfigField "String", "EMAIL", "\"release@six.ca\""
        resValue "string", "res_sample", "hello world"
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors{
        google {
            manifestPlaceholders = [APP_TAG: 'google']
        }
        anzhi {
            manifestPlaceholders = [APP_TAG: 'anzhi']
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        debug {
            storeFile file(properties.getProperty("DEBUG_STORE_FILE"))
            storePassword properties.getProperty("DEBUG_STORE_PASSWORD")
            keyAlias properties.getProperty("DEBUG_KEY_ALIAS")
            keyPassword properties.getProperty("DEBUG_KEY_PASSWORD")
        }
        release {
            storeFile file(properties.getProperty("RELEASE_STORE_FILE"))
            storePassword properties.getProperty("RELEASE_STORE_PASSWORD")
            keyAlias properties.getProperty("RELEASE_KEY_ALIAS")
            keyPassword properties.getProperty("RELEASE_KEY_PASSWORD")
        }
    }
}

def gitVersion() {
    if (!System.getenv('BUILD_NUMBER')) {
        return 1
    }
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
}
